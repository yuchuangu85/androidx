/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import static androidx.build.dependencies.DependenciesKt.*
import androidx.build.LibraryVersions
import androidx.build.LibraryGroups
import androidx.build.Publish

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
}

dependencies {
    implementation("androidx.test:core:1.2.0")
    implementation(ANDROIDX_TEST_RULES)
    implementation(ANDROIDX_TEST_UIAUTOMATOR)
    api("androidx.annotation:annotation:1.0.0")
    implementation(GUAVA_LISTENABLE_FUTURE)
    implementation("androidx.appcompat:appcompat:1.1.0")
    api(project(":camera:camera-core"))
    implementation("androidx.core:core:1.1.0")
    implementation("androidx.concurrent:concurrent-futures:1.0.0")
    implementation("androidx.test.espresso:espresso-idling-resource:3.1.0")
    implementation(JUNIT)

    testImplementation(ANDROIDX_TEST_CORE)
    testImplementation(ANDROIDX_TEST_RUNNER)
    testImplementation(JUNIT)
    testImplementation(TRUTH)
    testImplementation(ROBOLECTRIC)
    testImplementation(MOCKITO_CORE)
}

android {
    defaultConfig {
        minSdkVersion 21

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17"
                arguments "-DCMAKE_VERBOSE_MAKEFILE=ON"
            }
        }
    }

    // Use Robolectric 4.+
    testOptions.unitTests.includeAndroidResources = true

    externalNativeBuild {
        cmake {
            // AGP doesn't allow us to use project.buildDir (or subdirs) for CMake's generated
            // build files (ninja build files, CMakeCache.txt, etc.). Use a staging directory that
            // lives alongside the project's buildDir.
            buildStagingDirectory "${project.buildDir}/../nativeBuildStaging"
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }
}

androidx {
    name = "Jetpack Camera Testing Library"
    publish = Publish.NONE
    mavenGroup = LibraryGroups.CAMERA
    inceptionYear = "2019"
    description = "Testing components for the Jetpack Camera Library, a library providing a " +
            "consistent and reliable camera foundation that enables great camera driven " +"" +
            "experiences across all of Android."
}
