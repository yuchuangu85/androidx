/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.LibraryGroups
import androidx.build.LibraryVersions
import androidx.build.Publish
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import static androidx.build.dependencies.DependenciesKt.*

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXUiPlugin")
    id("kotlin-multiplatform")
}

kotlin {
    android()
    jvm("desktop")

    sourceSets {
        commonMain.dependencies {
            implementation(KOTLIN_STDLIB_COMMON)

            api project(":compose:ui:ui-unit")
            implementation project(":compose:ui:ui-util")
            implementation project(":compose:runtime:runtime")
        }

        androidMain.dependencies {
            api "androidx.annotation:annotation:1.1.0"
        }

        desktopMain.dependencies {
            implementation(KOTLIN_STDLIB)
            implementation(SKIKO)
        }

        androidTest.dependencies {
            implementation(ANDROIDX_TEST_RULES)
            implementation(ANDROIDX_TEST_RUNNER)
            implementation(JUNIT)
            implementation(TRUTH)
        }

        androidAndroidTest.dependencies {
            implementation project(":compose:ui:ui-graphics:samples")
            implementation(ANDROIDX_TEST_RULES)
            implementation(ANDROIDX_TEST_RUNNER)
            implementation(ESPRESSO_CORE)
            implementation(JUNIT)
            implementation project(":ui:ui-test")
        }
    }
}

androidx {
    name = "Compose Graphics"
    publish = Publish.SNAPSHOT_AND_RELEASE
    mavenVersion = LibraryVersions.UI
    mavenGroup = LibraryGroups.Compose.UI
    inceptionYear = "2020"
    description = "Compose graphics"
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += [
            "-XXLanguage:+InlineClasses",
            "-Xallow-jvm-ir-dependencies"
        ]
    }
}

android {
    buildTypes {
        debug {
            testCoverageEnabled = false
        }
        release {
            testCoverageEnabled = false
        }
    }
}
