/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.LibraryGroups
import androidx.build.LibraryVersions
import androidx.build.Publish
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import static androidx.build.dependencies.DependenciesKt.*

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXUiPlugin")
    id("kotlin-multiplatform")
}

dependencies {
    kotlinPlugin project(path: ":compose:compose-compiler")
}

kotlin {
    android()
    jvm("desktop")

    sourceSets {
        commonMain.dependencies {
            implementation(KOTLIN_STDLIB_COMMON)
            api project(":compose:runtime:runtime")
            implementation project(":compose:ui:ui-util")
            api project(":compose:ui:ui")
            api project(":compose:foundation:foundation")
            api project(":compose:material:material-icons-core")
            api project(":compose:ui:ui-text")
            api project(":compose:foundation:foundation-text")
            api project(":compose:animation:animation-core")

            implementation project(":compose:foundation:foundation-layout")
            implementation project(":compose:animation:animation")
        }

        androidMain.dependencies {
            api "androidx.annotation:annotation:1.1.0"
        }

        desktopMain.dependencies {
            implementation(KOTLIN_STDLIB)
            implementation(SKIKO)
        }

        test.dependencies {
            implementation(ANDROIDX_TEST_RULES)
            implementation(ANDROIDX_TEST_RUNNER)
            implementation(JUNIT)
            implementation(TRUTH)
        }

        androidAndroidTest.dependencies {
            implementation project(":compose:material:material:samples")
            implementation project(":ui:ui-test")
            implementation project(":test-screenshot")

            implementation(ANDROIDX_TEST_RULES)
            implementation(ANDROIDX_TEST_RUNNER)
            implementation(JUNIT)
            implementation(TRUTH)
            implementation(DEXMAKER_MOCKITO, libs.exclude_bytebuddy) // DexMaker has it"s own MockMaker
            implementation(MOCKITO_CORE, libs.exclude_bytebuddy) // DexMaker has it"s own MockMaker
            implementation MOCKITO_KOTLIN, {
                exclude group: 'org.mockito' // to keep control on the mockito version
            }
        }
    }
}

androidx {
    name = "Compose Material Components"
    publish = Publish.SNAPSHOT_AND_RELEASE
    mavenVersion = LibraryVersions.UI
    mavenGroup = LibraryGroups.Compose.MATERIAL
    inceptionYear = "2018"
    description = "Compose Material Design Components library"
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        useIR = true
    }
}

// Screenshot tests related setup
android {
    defaultConfig {
        testInstrumentationRunnerArgument("thisisignored", "thisisignored --no-isolated-storage")
    }
    sourceSets.androidTest.assets.srcDirs +=
            project.rootDir.absolutePath + "/../../../golden/compose/material/material"
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += [
                "-Xallow-jvm-ir-dependencies"
        ]
    }
}
