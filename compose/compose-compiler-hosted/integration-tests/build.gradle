/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import static androidx.build.dependencies.DependenciesKt.*
import androidx.build.LibraryGroups
import androidx.build.LibraryVersions
import androidx.build.Publish

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("kotlin-android")
}

dependencies {
    implementation(KOTLIN_STDLIB)
    testImplementation(JUNIT)
    testImplementation(ROBOLECTRIC)

    testImplementation("org.jetbrains.kotlin:kotlin-compiler:$KOTLIN_VERSION")

    testImplementation(KOTLIN_STDLIB)
    testImplementation(project(":compose:androidview:androidview"))
    testImplementation(project(":compose:compose-compiler-hosted"))
    testImplementation(project(":compose:runtime:runtime"))
    testImplementation(project(":compose:ui:ui"))
    testImplementation("androidx.core:core-ktx:1.1.0")
}

android {
    defaultConfig {
        minSdkVersion 16
    }
    lintOptions {
        disable("SyntheticAccessor")
    }
    testOptions {
        unitTests.all {
            // There is only one version of the compose plugin built so the debug tests are
            // sufficient as they test that one version
            if (it.name == 'testReleaseUnitTest') {
                filter {
                    exclude '*'
                }
            }
        }
    }
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn", "-Xallow-jvm-ir-dependencies"]
    }
}

androidx {
    name = "AndroidX Compiler CLI Tests"
    publish = Publish.NONE
    toolingProject = true
    mavenGroup = LibraryGroups.COMPOSE
    inceptionYear = "2019"
    description = "Contains test for the compose compiler plugin"
}
